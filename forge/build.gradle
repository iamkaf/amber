plugins {
    id 'multiloader-loader'
    // id 'net.minecraftforge.accesstransformers' version '5.0.1'
    id 'net.minecraftforge.gradle' version '7.0.0-beta.51'
    id 'net.minecraftforge.jarjar' version '0.2.3'
    //id 'org.spongepowered.mixin' version '0.7-SNAPSHOT'
    id 'com.hypherionmc.modutils.modpublisher'
    id 'idea'
}

/*
mixin {
    config("${mod_id}.mixins.json")
    config("${mod_id}.forge.mixins.json")
}
*/

repositories {
    maven minecraft.mavenizer
    maven fg.forgeMaven
    maven fg.minecraftLibsMaven
}

jar {
    manifest {
        attributes([
            "MixinConfigs" : "amber.forge.mixins.json"
        ])
    }
}

minecraft {
    mappings channel: 'official', version: minecraft_version

    // Automatically enable forge AccessTransformers if the file exists
    // This location is hardcoded in Forge and can not be changed.
    // https://github.com/MinecraftForge/MinecraftForge/blob/be1698bb1554f9c8fa2f58e32b9ab70bc4385e60/fmlloader/src/main/java/net/minecraftforge/fml/loading/moddiscovery/ModFile.java#L123
    // Forge still uses SRG names during compile time, so we cannot use the common AT's
    def at = file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformer = at
    }

    runs {
        // applies to all the run configs below
        configureEach {
            workingDir.convention layout.projectDirectory.dir('run')

            //systemProperty 'forge.logging.markers', 'REGISTRIES'

            systemProperty 'forge.logging.console.level', 'debug'

            systemProperty 'eventbus.api.strictRuntimeChecks', 'true'

            args "-mixin.config=${mod_id}.forge.mixins.json"
        }

        client {
            workingDir.convention layout.projectDirectory.dir('runs/client')
        }

        server {
            workingDir.convention layout.projectDirectory.dir('runs/server')
            args '--nogui'
        }
    }
}

sourceSets.main.resources.srcDir 'src/generated/resources'

// IntelliJ no longer downloads javadocs and sources by default, this tells Gradle to force IntelliJ to do it.
idea.module { downloadJavadoc = downloadSources = true }

dependencies {
    implementation minecraft.dependency("net.minecraftforge:forge:$minecraft_version-$forge_version")
    // This is currently bugged due to the absence of obfuscation mappings
    // annotationProcessor("org.spongepowered:mixin:0.8.7-SNAPSHOT:processor")

    // Event Bus 7
    annotationProcessor 'net.minecraftforge:eventbus-validator:7.0-beta.10'
}

sourceSets.each {
    def dir = layout.buildDirectory.dir("sourcesSets/$it.name")
    it.output.resourcesDir = dir
    it.java.destinationDirectory = dir
}

publisher {
    // Setup the required API keys. You only need to define the keys for
    // the platforms you plan on uploading to
    apiKeys {
        // Modrinth Token
        modrinth System.getenv("MODRINTH_TOKEN")
        // Curseforge Token
        curseforge System.getenv("CURSEFORGE_TOKEN")
    }

    // Enable Debug mode. When enabled, no files will actually be uploaded
    setDebug(Boolean.valueOf(dry_run))

    // Curseforge Project ID
    setCurseID(curse_id)

    // Modrinth Project ID
    setModrinthID(modrinth_id)

    // Type of release. beta, alpha or release
    // You can also use VersionType.BETA, VersionType.ALPHA or VersionType.RELEASE
    setVersionType(release_type)

    setChangelog(extractHeaderLatestFooterFromChangelog(file('../changelog.md').text))

    // Required for Modrinth/GitHub
    setProjectVersion(rootProject.version)

    // Fancy display name for the upload.
    // Will default to the project version if not set
    setDisplayName("$mod_id-$project.name-$project.version")

    // The supported game versions
    setGameVersions(game_versions.split(','))

    // The modloaders your upload supports.
    // This can also be an Enum from ModLoader,
    // like setLoaders(ModLoader.FABRIC, ModLoader.FORGE)
    setLoaders("forge")

    // The new Curseforge Environment tag. Optional
    // Valid values are "server", "client" or "both"
    // You can also use CurseEnvironment.BOTH, or CurseEnvironment.SERVER or CurseEnvironment.CLIENT
    setCurseEnvironment(mod_environment)

    // Upload the release, but keep it "private" until you are ready
    // Unreleased feature on CurseForge, working on Modrinth
    setIsManualRelease(false)

    // The file to be uploaded. This can be a file, task, or string.
    // setArtifact("build/libs/mymod.jar")
    // setArtifact(jar.getArchiveFile().get())
    // If this is a task, the task specified will be executed before publishing
    setArtifact("build/libs/${mod_id}-${project.name}-${project.version}.jar")
    setDisableEmptyJarCheck(false)

    // Add supported java versions. Currently only used by CurseForge
    // Supports anything that can be parsed using JavaVersion.toVersion()
    setJavaVersions(JavaVersion.VERSION_21, JavaVersion.VERSION_22)
}
