plugins {
    id 'multiloader-common'
}

configurations {
    commonJava{
        canBeResolved = true
    }
    commonResources{
        canBeResolved = true
    }
}

dependencies {
    implementation(project(':common')) {
        capabilities {
            requireCapability "$group:$mod_id"
        }
    }
    compileOnly(project(':common')) {
        capabilities {
            requireCapability "$group:$mod_id"
        }
    }
    commonJava project(path: ':common', configuration: 'commonJava')
    commonResources project(path: ':common', configuration: 'commonResources')
}

tasks.named('compileJava', JavaCompile) {
    dependsOn(configurations.commonJava)
    source(configurations.commonJava)
    // Include common's compile classpath (for Minecraft and other dependencies)
    classpath += project(':common').configurations.compileClasspath
    // Also include this module's own compile classpath (for loader-specific dependencies like Fabric API)
    classpath += configurations.compileClasspath
}

processResources {
    dependsOn(configurations.commonResources)
    from(configurations.commonResources)
}

tasks.named('javadoc', Javadoc).configure {
    dependsOn(configurations.commonJava)
    source(configurations.commonJava)
    // Include common's compile classpath so javadoc can find Minecraft classes
    classpath = project(':common').configurations.compileClasspath + configurations.compileClasspath
    options.addBooleanOption('Xdoclint:none', true)
    options.addBooleanOption('quiet', true)
}

tasks.named('sourcesJar', Jar) {
    dependsOn(configurations.commonJava)
    from(configurations.commonJava)
    dependsOn(configurations.commonResources)
    from(configurations.commonResources)
}
