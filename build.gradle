plugins {
    id 'dev.architectury.loom' version '1.10-SNAPSHOT' apply false
    id 'architectury-plugin' version '3.4-SNAPSHOT'
    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
}

println("Mod Resources: ${System.getenv("MOD_RESOURCES")}")

apply from: "https://raw.githubusercontent.com/iamkaf/modresources/refs/heads/main/gradle/v0/main.gradle"

/**
 * Extracts:
 *  – the intro/header                (everything before the first version heading)
 *  – the newest version section      (## <semver> … until the next version OR non-numeric heading)
 *  – the footer                      (from '## Types of changes' to EOF)
 */
static String extractHeaderLatestFooterFromChangelog(String completeChangelog) {

    // 1) header: up to the first version heading
    def header = (completeChangelog =~ /(?ms)\A.*?(?=^## \d+\.\d+\.\d+)/)[0]

    // 2) latest version block: from that heading until
    //    – the next version heading  OR
    //    – the first non-numeric heading (e.g. 'Types of changes')  OR
    //    – end of file
    def latest = (completeChangelog =~ /(?ms)^## \d+\.\d+\.\d+[\s\S]*?(?=^## (?:\d+\.\d+\.\d+|[^0-9]|$))/)[0]

    // 3) footer: everything from '## Types of changes' (or any other marker you prefer)
    def footer = (completeChangelog =~ /(?ms)^## Types of changes[\s\S]*/)[0]

    return header + latest + footer
}

subprojects {
    // Configure Maven publishing.
    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId = "${mod_group}"
                artifactId = "${mod_id}-${project.name}"
                from components.java
            }
        }

        // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
        repositories {
            // Add repositories to publish to here.
            // Notice: This block does NOT have the same function as the block in the top level.
            // The repositories here will be used for publishing your artifact, not for
            // retrieving dependencies.
            maven {
                // change to point to your repo, e.g. http://my.org/repo
                url = file(System.getenv("MOD_RESOURCES").concat(File.separator).concat("maven"))
            }
        }
    }
}